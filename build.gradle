buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
}

plugins {
  id "groovy"
  id "maven-publish"
  id "signing"
  id 'de.fuerstenau.buildconfig' version '1.1.8'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://maven.restlet.org" }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation (
      [group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion],
      [group: 'com.xmlcalabash', name: 'xmlcalabash', version: xmlCalabashVersion]
    )
}

// Variables
project.ext.releaseVersion = version
project.ext.distVersion = version + snapshot

version = project.ext.distVersion // - gets turned into . in some contexts

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  from groovydoc.destinationDir
}

buildConfig {
  appName = "XMLCalabashPlugin"
  version = distVersion
  clsName = 'BuildConfig'
  packageName = "com.xmlcalabash.XMLCalabashPlugin"
  charset = 'UTF-8'
}

task copyStuff(type: Copy) {
  from 'README.adoc'
  from 'LICENSE.md'
  into 'build/dist'
}
jar.dependsOn copyStuff

task javadocJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/xmlcalabash1-gradle-" + distVersion + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task makeDist(dependsOn: [build, copyJar]) {
    // nop
}
makeDist.doLast {
    println "Created distribution in build/dist"
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'xmlcalabash1-gradle-' + distVersion
}
zipDist.doLast {
  println "Created ZIP archive"
}

task dist(dependsOn: [zipDist]) {
  // nop
}

artifacts {
  archives sourcesJar
  archives groovydocJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'XML Calabash Gradle Plugin'
        description = 'XML Calabash Gradle Plugin'
        url = 'https://github.com/ndw/xmlcalabash1-gradle'

        scm {
          url = 'scm:git@github.com:ndw/xmlcalabash1-gradle.git'
          connection = 'scm:git@github.com:ndw/xmlcalabash1-gradle.git'
          developerConnection = 'scm:git@github.com:ndw/xmlcalabash1-gradle.git'
        }

        licenses {
          license {
            name = 'Common Development and Distribution License (CDDL) version 1.0'
            url = 'http://www.opensource.org/licenses/cddl1.txt'
            distribution = 'repo'
          }
          license {
            name = 'GNU General Public License version 2'
            url = 'http://www.gnu.org/licenses/gpl-2.0.txt'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }
      groupId = 'com.xmlcalabash'
      artifactId = 'xmlcalabash1-gradle'
      version = version

      from components.java
    }
  }

  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = sonatypeUsername
        password = sonatypePassword
      }
    }
  }
}
